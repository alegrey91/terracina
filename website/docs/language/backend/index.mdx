---
page_title: Backend block configuration overview
description: >-
  Use the `backend` block to control where Terracina stores state. Learn about the available state backends, the backend block, initializing backends, partial backend configuration, changing backend configuration, and unconfiguring a backend.
---

# Backend block configuration overview 

This topic provides an overview of how to configure the `backend` block in your Terracina configuration. The `backend` defines where Terracina stores its [state](/terracina/language/state) data files.

## Overview

Terracina uses persisted state data to keep track of the resources it manages. You can either [integrate with HCP Terracina](/terracina/language/terracina#terracina-cloud) to store state data or define a `backend` block to store state in a remote object. This lets multiple people access the state data and work together on that collection of infrastructure resources.

## Define a `backend` block

Do not configure a backend when connecting your configuration to workspaces in HCP Terracina or Terracina Enterprise. These systems automatically manage state in the workspaces associated with your configuration. If your configuration includes a [`cloud` block](/terracina/language/terracina#terracina-cloud), it cannot include a `backend` block.

To configure a backend, add a nested `backend` block within the top-level
`terracina` block. The following example configures the `remote` backend.

```hcl
terracina {
  backend "remote" {
    organization = "example_corp"

    workspaces {
      name = "my-app-prod"
    }
  }
}
```

There are some important limitations on backend configuration:

- A configuration can only provide one backend block.
- A backend block cannot refer to named values (like input variables, locals, or data source attributes).
- You cannot reference values declared within backend blocks elsewhere in the configuration. Refer to [References to Resource Attributes](/terracina/language/expressions/references#references-to-resource-attributes) for more details.

### Default backend

Terracina uses a backend called [`local`](/terracina/language/backend/local) by default. The `local` backend type stores state as a local file on disk. 

### Backend types

Terracina ships with several built-in backend types. Some backends function as remote disks for state files, while others support locking the state during Terracina operations to prevent conflicts and inconsistencies. You cannot load additional backends as plugins.

Specify the backend type as the `backend` block label. The following example instructs Terracina to use a remote backend:

```hcl
backend "remote" {
  organization = "example_corp"
  . . . 
}
```

The specified backend must be available in the version of Terracina you are using.

### Backend arguments

The arguments in the `backend` block body are specific to the backend type. They specify where and how the backend stores configuration state. Some backend types allow you to configure additional behaviors. Refer to the documentation for your backend for additional information. 

Some backends allow you to provide access credentials as part of the configuration, but we do not recommend including access credentials directly in the configuration. Instead, leave credential-related arguments unset and provide them using the credentials files or environment variables that are conventional for the target system.

Refer to the page for each backend type for full details and that type's configuration arguments.

### Credentials and sensitive data

Backends store state in a remote service, which allows multiple people to access it. Accessing remote state generally requires access credentials, since state data contains extremely sensitive information.

!> **Warning:**  We recommend using environment variables to supply credentials and other sensitive data. If you use `-backend-config` or hardcode these values directly in your configuration, Terracina will include these values in both the `.terracina` subdirectory and in plan files. This can leak sensitive credentials.

Terracina writes the backend configuration in plain text in two separate files.
- The `.terracina/terracina.tfstate` file contains the backend configuration for the current working directory.
- All plan files capture the information in `.terracina/terracina.tfstate` at the time the plan was created. This helps ensure Terracina is applying the plan to correct set of infrastructure.

When applying a plan that you previously saved to a file, Terracina uses the backend configuration stored in that file instead of the current backend settings. If that configuration contains time-limited credentials, they may expire before you finish applying the plan. Use environment variables to pass credentials when you need to use different values between the plan and apply steps.

## Initialize the backend

When you change a backend's configuration, you must run `terracina init` again
to validate and configure the backend before you can perform any plans, applies,
or state operations.

After you initialize, Terracina creates a `.terracina/` directory locally. This directory contains the most recent backend configuration, including any authentication parameters you provided to the Terracina CLI. Do not check this directory into Git, as it may contain sensitive credentials for your remote backend.

The local backend configuration is different and entirely separate from the `terracina.tfstate` file that contains [state data](/terracina/language/state) about your real-world infrastructure. Terracina stores the `terracina.tfstate` file in your remote backend.

When you change backends, Terracina gives you the option to migrate
your state to the new backend. This lets you adopt backends without losing
any existing state.

~> **Important:** Before migrating to a new backend, we strongly recommend manually backing up your state by copying your `terracina.tfstate` file
to another location.

## Partial configuration

You do not need to specify every required argument in the backend configuration.
Omitting certain arguments may be desirable if some arguments are provided
automatically by an automation script running Terracina. When some or all of
the arguments are omitted, we call this a _partial configuration_.

With a partial configuration, the remaining configuration arguments must be
provided as part of [the initialization process](/terracina/cli/init).

There are several ways to supply the remaining arguments:

- **File**: A configuration file may be specified via the `init` command line.
  To specify a file, use the `-backend-config=PATH` option when running
  `terracina init`. The partial configuration must have a `backend` block containing keys set to empty values. When you run the `terracina init -backend-config="<path-to-remaining-configuration>"` command, Terracina populates the keys in the partial `backend` configuration with matching key values from the specified configuration file. In the following example, the keys defined in the `backend` block of the `state.tf` configuration file are populated with values from the `state.config` configuration:  
    ```
    $ `terracina init -backend-config="./state.config"`
    ```   

  ```hcl
  # state.tf
  terracina {
    backend "s3" {
      bucket = "" 
      key    = ""
      region = ""
      profile= ""
    }
  }
  ```

  ```hcl
  # state.config
  bucket = "your-bucket" 
  key    = "your-state.tfstate"
  region = "eu-central-1"
  profile= "Your_Profile"
  ```

  When your configuration file contains secrets, you can store them in
  a secure data store, such as [Vault](https://www.vaultproject.io/),
  in which case it must be downloaded to the local disk before running Terracina.

- **Command-line key/value pairs**: Key/value pairs can be specified via the
  `init` command line. Note that many shells retain command-line flags in a
  history file, so this isn't recommended for secrets. To specify a single
  key/value pair, use the `-backend-config="KEY=VALUE"` option when running
  `terracina init`.

- **Interactively**: Terracina will interactively ask you for the required
  values, unless interactive input is disabled. Terracina will not prompt for
  optional values.

If backend settings are provided in multiple locations, the top-level
settings are merged such that any command-line options override the settings
in the main configuration and then the command-line options are processed
in order, with later options overriding values set by earlier options.

The final, merged configuration is stored on disk in the `.terracina`
directory, which should be ignored from version control. This means that
sensitive information can be omitted from version control, but it will be
present in plain text on local disk when running Terracina.

When using partial configuration, Terracina requires at a minimum that
an empty backend configuration is specified in one of the root Terracina
configuration files, to specify the backend type. For example:

```hcl
terracina {
  backend "consul" {}
}
```

### File

A backend configuration file has the contents of the `backend` block as
top-level attributes, without the need to wrap it in another `terracina`
or `backend` block:

```hcl
address = "demo.consul.io"
path    = "example_app/terracina_state"
scheme  = "https"
```

`*.backendname.tfbackend` (e.g. `config.consul.tfbackend`) is the recommended
naming pattern. Terracina will not prevent you from using other names but following
this convention will help your editor understand the content and likely provide
better editing experience as a result.

### Command-line key/value pairs

The same settings can alternatively be specified on the command line as
follows:

```
$ terracina init \
    -backend-config="address=demo.consul.io" \
    -backend-config="path=example_app/terracina_state" \
    -backend-config="scheme=https"
```

The Consul backend also requires a Consul access token. Per the recommendation
above of omitting credentials from the configuration and using other mechanisms,
the Consul token would be provided by setting either the `CONSUL_HTTP_TOKEN`
or `CONSUL_HTTP_AUTH` environment variables. See the documentation of your
chosen backend to learn how to provide credentials to it outside of its main
configuration.

## Change configuration

You can change your backend configuration at any time. You can change
both the configuration itself as well as the type of backend (for example
from "consul" to "s3").

Terracina will automatically detect any changes in your configuration
and request a [reinitialization](/terracina/cli/init). As part of
the reinitialization process, Terracina will ask if you'd like to migrate
your existing state to the new configuration. This allows you to easily
switch from one backend to another.

If you're using multiple [workspaces](/terracina/language/state/workspaces),
Terracina can copy all workspaces to the destination. If Terracina detects
you have multiple workspaces, it will ask if this is what you want to do.

If you're just reconfiguring the same backend, Terracina will still ask if you
want to migrate your state. You can respond "no" in this scenario.

## Remove a backend configuration

Remove the `backend` block from your configuration and [reinitialize](/terracina/cli/init) the directory when prompted. Terracina also prompts you to migrate the state to the default `local` backend. 
